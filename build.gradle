buildscript {
    ext {
        junit5Version = "5.8.2" // https://junit.org/junit5/
    }
    repositories {
        mavenLocal()
        mavenCentral()
        google()

        maven { url "https://plugins.gradle.org/m2" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
}
allprojects {
    version = '1.0-SNAPSHOT'
    repositories {
        mavenLocal()
        mavenCentral()
        google()

        maven { url "https://repo.maven.apache.org/maven2" }
        maven { url "https://plugins.gradle.org/m2" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://frcmaven.wpi.edu/artifactory/release" }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    tasks.withType(JavaCompile) {
    	options.encoding = 'UTF-8'
    }
    dependencies {
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit5Version
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit5Version
    }
    test {
        useJUnitPlatform()
    }

    task packageSources(type: Jar) {
        archiveClassifier.set("sources")
        from sourceSets.main.allSource
    }
    artifacts.archives packageSources

    publishing {
        publications {
            mavenJava(MavenPublication) {
                // thanks https://stackoverflow.com/a/68515859/5434860
                groupId = "${group}"
                artifactId = "${project.name}"
                version = "${version}"

                from project.components.java
            }
        }
    }
}
project(":api"){
    apply plugin: 'java-library'
    sourceCompatibility = 8
    targetCompatibility = 8
}
project(":gdx-common"){
    apply plugin: 'java-library'
    sourceCompatibility = 8
    targetCompatibility = 8
    dependencies {
        api project(":api")
    }
}
project(":gdx"){
    apply plugin: 'java-library'
    sourceCompatibility = 8
    targetCompatibility = 8
    dependencies {
        api project(":api")
        api project(":gdx-common")
    }
}
project(":gdx2"){
    apply plugin: 'java-library'
    sourceCompatibility = 8
    targetCompatibility = 8
    dependencies {
        api project(":api")
        api project(":gdx-common")
    }
}

wrapper {
    gradleVersion = '7.3.2' // https://gradle.org/releases/
    distributionType = Wrapper.DistributionType.ALL
}
